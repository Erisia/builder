with import <nixpkgs> {};
with stdenv;

with import ./lib/lib.nix;

let
  forgeMajor = "1.7.10";
  forgeMinor = "10.13.4.1566";
  common-mods = {
    chunkgen = mkMod {
      name = "chunkgen-1.2.4-dev";
      src = fetchurl {
        url = https://madoka.brage.info/baughn/chunkgen-1.7.10-1.2.3+10.jar;
        sha256 = "0pd77rzghp3fh1pnvk7paws2ksk6alx0riar9wk2b4g67wmn8sn6";
      };
      side = "SERVER";
    };

    dynmap = mkMod {
      name = "dynmapforge";
      src = fetchurl {
        url = https://bloxgaming.com/bloxgate/Dynmap-2.2-forge-1.7.10.jar;
        sha256 = "d6391dc83af1cd2ec51ffceb4e6bb58bf1419a96336ca92360acfd90a91ce9b9";
      };
      side = "SERVER";
    };
    
    PrometheusIntegration = mkMod {
      name = "PrometheusIntegration-1.1.0";
      src = fetchurl {
        url = https://madoka.brage.info/baughn/prometheus-integration-1.1.0.jar;
        sha256 = "b38cdb14dd571fc6e257737d2c5464ec8b9f1fbf942b4da2bfab736dd309f247";
      };
      side = "SERVER";
    };

    Terminator = mkMod {
      name = "Terminator-1.0";
      src = fetchurl {
        url = https://madoka.brage.info/baughn/terminator-1.0.jar;
        sha256 = "8b56b632d09eeb24c52bfeb4fda576f99490c97d8d26f88cd489869c4dc38c95";
      };
      side = "SERVER";
    };

    ElectricalAge = mkMod rec {
      ver = "51.26";
      name = "ElectricalAge-${ver}";
      src = fetchurl {
        url = "https://madoka.brage.info/baughn/ElectricalAge-${ver}.jar";
        sha256 = "8fe075ccbfe3194a4b0813f1413b327fba2300836de3600a0a9f97a7404ae918";
      };
    };

    TickProfiler = mkMod {
      name = "TickProfiler-1.7.10-jenkins-30";
      src = fetchurl {
        url = https://jenkins.nallar.me/job/TickProfiler/branch/1.7.10/31/artifact/build/libs/TickProfiler-1.7.10.jenkins.31.jar;
        sha256 = "0lns8lsx63s752i8zfsr8r84cyp4n23g4k6cn5y6i016s34ab4ln";
      };
      side = "SERVER";
    };
  };
in

rec {

  servers = {
    erisia-14 = {
      serverId = "erisia-14";
      serverDesc = "Erisia #14: Cave Creeperum";
      server = mm-server;
      port = 25565;
      hacks = {
        enableAntiChunkChurn = false;
        saveTime = 45;
      };
    };
  };

  ServerPack = mkServerPack {
    inherit forgeMajor forgeMinor servers;
  };

  web = callPackage ./web {};

  ###############################
  ## Erisia 14: Modular Mayhem ##
  ###############################

  mm = mkBasePack {

    src = (mkCursePack {
      manifest = manifests/Modular_Mayhem-1.21.zip;
      # Generated by scraping Curse.
      updates = [
        manifests/ModularMayhem.nix
        manifests/ModularMayhemExtras.nix
        manifests/Cosmetic.nix
        manifests/fixed-base.nix
        manifests/base.nix
      ];
    }).pack;

    modConfig = {
      fastcraft = {
        required = false;
        side = "CLIENT";
        isDefault = true;
      };
      journeymap = {
        required = false;
        isDefault = true;
      };
      BetterFoliage = {
        side = "CLIENT";
        required = false;
      };
      DynamicLights = {
        side = "CLIENT";
        required = false;
        isDefault = true;
      };
      DynamicSurroundings = {
        side = "CLIENT";
        required = false;
        isDefault = true;
      };
      SoundFilters = {
        side = "CLIENT";
        required = false;
        isDefault = true;
      };
      Fullscreen_Windowed = {
        side = "CLIENT";
        required = false;
        isDefault = true;
      };
      morpheus = {
        side = "SERVER";
      };
    };
  };

  mm-mods = (builtins.removeAttrs mm.mods [
    "GalacticraftCore"
    "Galacticraft-Planets"
    "GalaxySpace"
    "MicdoodleCore"
    "extrabiomesxl"
    "StevesWorkshop-"  # Duplicate of Ewy's.
    "CyanosLootableBodies"  # Buggy.
  ]) // common-mods // {
    # Paintings!
    BiblioCraft = bibliocraftWithPaintings {
      bibliocraft = mm.mods.BiblioCraft;
      paintings = ./extraPaintings;
    };

    # Remove the outdated ComputerCraft API so ChromatiCraft won't crash.
    EngineersToolbox = removeAPI {
      mod = mm.mods.EngineersToolbox;
      path = "dan200";
    };
    tardismod = removeAPI {
      mod = mm.mods.tardismod;
      path = "dan200";
    };

    # Terrain generation, mostly in fixed-base.nix.
    # These should generally *not* be updated, as it'd break the terrain.
    WildCaves = mkMod {
      name = "WildCaves-3";
      src = fetchurl {
        url = "https://madoka.brage.info/baughn/WildCaves3-0.4.3.7.jar";
        sha256 = "1yc71myww1l80xpl2wj954j43z1d1cvb40yfqgnlp5z2rdrcgfac";
      };
    };
    
    # Books.
    LostBooks = mkMod {
      name = "LostBooks";
      src = fetchurl {
        url = https://madoka.brage.info/baughn/LostBooks-1.7.10-1.2.2.jar;
        sha256 = "1mf8amqq591idzid7qsqik3kb98s7nb0iihzyb5vcl92n6pdn9m9";
      };
    };

    # Optifine = tfp-mods.Optifine;

    # Fixes the shelving units.
    MagneticraftPatcher = mkMod {
      name = "MagneticraftPatcher";
      src = fetchurl {
        url = https://github.com/Belgabor/MagneticraftPatcher/releases/download/Release_v0.1/MagneticraftPatcher-1.7.10-0.1.jar;
        sha256 = "1grdgdp4clvf5mldkva0wrdkmcdn4rvx5q2zc1mg0m5xb9v67w7s";
      };
    };
  };

  mm-server = mkServer {
    name = "erisia-14";

    mods = mm-mods;

    inherit forgeMajor;
    forge = fetchForge {
      major = forgeMajor; minor = forgeMinor;
      sha1 = "4d2xzm7w6xwk09q7sbcsbnsalc09xp0v";
    };

    screenName = "e14";
    hacks = servers.erisia-14.hacks;

    # These are applied in order. In case of overwrites nothing is deleted.
    # They're also copied to the client, after applying the below patches.
    extraDirs = [
      (mm.getDir "config")
      (mm.getDir "scripts")
      (bevos.getDir "libraries")
      ./base
      ./base-e14
    ];

    # These are applied after everything else.
    # And in order, if it matters.
    # TODO: Write something that understands what it's doing.
    configPatches = [
      # Keep the SD behaviour we're used to.
      ''sed -i StorageDrawers.cfg -e s/B:invertShift=false/B:invertShift=true/''
      # Overpowered worldgen is overpowered.
      ''sed -i "MatterOverdrive/Matter Overdrive.cfg" -e 's/"generate buildings"=true/"generate buildings"=false/' ''
      # Kill the annoying analytics spam.
      ''sed -i -e "s/analytics=true/analytics=false/" *.cfg */*.cfg''
      ''sed -i -e "s/usageStatistics=true/usageStatistics=false/" *.cfg */*.cfg''
      # Enable OpenBlocks graves
      ''sed -i -e "s/B:grave=false/B:grave=true/" OpenBlocks.cfg''
      ''sed -i -e "s/B:destructiveGraves=false/B:destructiveGraves=true/" OpenBlocks.cfg''
      # Surpress COFH Deathspam.
      ''sed -i "cofh/core/common.cfg" -e 's/B:EnableGenericDeathMessage=true/B:EnableGenericDeathMessage=false/' ''
      # Remove heat blocks; fix CC pylon rebuilding
      ''sed -i "railcraft/blocks.cfg" -e 's/B:residual.heat=true/B:residual.heat=false/' ''
      # So many client configs.
      ''find . | grep -i client | xargs rm''
      # Chunkloaders do not need fuel
      ''sed -i immibis.cfg -e s/B:chunkloader.useFuel=true/B:chunkloader.useFuel=false/''
      ''sed -i railcraft/railcraft.cfg -e "s/S:passive.fuel=minecraft:ender_pearl=12/S:passive.fuel=/" ''
      ''sed -i railcraft/railcraft.cfg -e "s/S:personal.fuel=minecraft:ender_pearl=12/S:personal.fuel=/" ''
      ''sed -i railcraft/railcraft.cfg -e "s/S:world.fuel=minecraft:ender_pearl=12/S:world.fuel=/" ''
      
    ];
  };


  # Used for the libraries only. TODO: Remove.
  bevos = mkBasePack {
    src = fetchzip {
      url = https://madoka.brage.info/baughn/BevosUNC.zip;
      sha256 = "1w5ks89lga3lrv5gzc24sxx0szqryn111xn2k8zmb78v0vk8mmsc";
      stripRoot = false;
    };
  };
  
}
